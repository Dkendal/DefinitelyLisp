program = { SOI ~ NEWLINE* ~ statement* ~ NEWLINE* ~ EOI }

test_expr = _{ SOI ~ expr ~ EOI }

statement = {
    type_alias
  | interface
  | import_statement
  | unittest
}

import_statement = {
    "import" ~ import_clause ~ from_clause
}

from_clause = _{
    "from" ~ string
}

import_clause = _{
    named_import
  | namespace_import
}

namespace_import = {
    "*" ~ "as" ~ #alias = ident
}

unittest = {
    "unittest" ~ #name = string ~ "do" ~ #body = (expr)* ~ "end"
}

named_import = {
    "{" ~ (#import_specifier = import_specifier ~ ("," ~ #import_specifier = import_specifier)*)? ~ ","? ~ "}"
}

import_specifier = {
    #name = ident ~ ("as" ~ #alias = ident)?
}

type_alias = {
    (#export = export)? ~ "type" ~ 
    #name = ident ~
    definition_options ~
    "as" ~ #body = expr
}

interface = {
    (#export = export)? ~ "interface" ~
    #name = ident ~
    definition_options ~
    #extends = extends_clause? ~
    #body = object_literal
}


definition_options = _{
    ("(" ~ #parameters = type_parameters ~ ")")? ~
    #defaults = defaults_caluse? ~
    #where = where_clause?
}

extends_clause = {
    "extends" ~ #name = ident
}

defaults_caluse = {
    "defaults" ~ #default = type_parameter_default ~ ("," ~ #default = type_parameter_default)* ~ ","?
}

where_clause = {
    "where" ~ #constraint = type_constraint ~ ("," ~ #constraint = type_constraint)* ~ ","?
}

type_parameter_default = {
    #name = ident ~ "=" ~ #value = expr
}

type_constraint = {
    #constraint_name = ident ~ extends ~ #constraint_body = expr
}

type_parameters = { ident ~ ("," ~ ident)* }

expr = { expr1 ~ (expr_infix ~ expr1)* }

expr1 = _{ expr_prefix* ~ expr_primary ~ expr_postfix* }

expr_primary = _{
    if_expr
  | map_expr
  | match_expr
  | cond_expr
  | cond_expr
  | let_expr
  | builtin
  | macro_call
  | term
  | "(" ~ expr ~ ")"
}

expr_prefix = _{ infer }

expr_infix = _{ union | intersection | pipe | dot_op | colon2 }

expr_postfix = _{ indexed_access | array_modifier | application }

colon2 = { "::" }

dot_op = { "." }

array_modifier = @{ "[]" }

indexed_access = { "[" ~ expr ~ "]" }

builtin = { #name = builtin_keyword ~ "(" ~ #argument = expr ~ ")" }

builtin_keyword = _{ keyof | builtin_typeof }

keyof = @{ "keyof" }

builtin_typeof = @{ "typeof" }

application = { "(" ~ #arguments = argument_list ~ ")" }

macro_call = { macro_ident ~ "(" ~ #arguments = argument_list ~ ")" }

argument_list = { (expr ~ ("," ~ expr)* ~ ","?)? }

infer = @{ "?" }

neg = _{ "-" }

pipe = @{ "|>" }

union = @{ !pipe ~ "|" }

intersection = @{ "&" }

term = _{
    top_type
  | bottom_type
  | function_type
  | number
  | boolean
  | primitive
  | template_string
  | string
  | ident
  | tuple
  | object_literal
}

function_type = {
    parameters ~ "=>" ~ expr
}

parameters = {
    "(" ~ (named_parameters | unnamed_parameters)? ~ ")"
}

unnamed_parameters = {
    (unnamed_parameter ~ ("," ~ unnamed_parameter)* ~ ","?)
}

named_parameters = {
    (named_parameter ~ ("," ~ named_parameter)* ~ ","?)
}

named_parameter = {
    ellipsis_token? ~ #name = ident ~ ":" ~ #type = expr
}

ellipsis_token = { "..." }

unnamed_parameter = {
    ellipsis_token? ~ expr
}

boolean = {
    literal_true | literal_false
}

tuple = { "[" ~ (expr ~ ("," ~ expr)*)? ~ "]" }

top_type = _{ any | unknown }

bottom_type = _{ never }

object_literal = {
    "{" ~ (object_property ~ ("," ~ object_property)* ~ ","?)? ~ "}"
}

object_property = {
    property_key ~ ":" ~ #value = expr
}

property_key = {
    property_key_prefix ~ #key = property_key_inner
}

property_key_prefix = _{
    (#readonly = readonly_modifier)? ~ (#optional = optional_modifier)?
}

property_key_inner = _{
    ident
  | ("[" ~ (index_property_key | computed_property_key) ~ "]")
}

computed_property_key = { ident }

index_property_key = {
    #index = ident ~ "in" ~ #iterable = expr ~ ("as" ~ #remap_clause = expr)?
}

let_expr = {
    "let" ~ #binding = let_binding ~ ("," ~ #binding = let_binding)* ~ ","? ~ "in" ~ #body = expr
}

let_binding = {
    ident ~ "=" ~ expr
}

map_expr = {
    "map" ~ property_key_prefix ~ index_property_key ~ "do" ~ #body = expr ~ "end"
}

if_expr = {
    "if" ~ #condition = extends_expr ~ "then" ~ #then = expr ~ ("else" ~ #else = expr)? ~ "end"
}

match_expr = {
    "match" ~ #value = expr ~ "do" ~ #arm = match_arm ~ ("," ~ #arm = match_arm)* ~ ("," ~ #else = else_arm)? ~ ","? ~ "end"
}

match_arm = {
    #pattern = expr ~ "->" ~ #body = expr
}

cond_expr = {
    "cond" ~ "do" ~ #arm = cond_arm ~ ("," ~ #arm = cond_arm)* ~ ("," ~ #else = else_arm)? ~ ","? ~ "end"
}

cond_arm = {
    #condition = extends_expr ~ "->" ~ #body = expr
}

else_arm = {
    "else" ~ "->" ~ #body = expr
}

extends_expr = {
    extends_prefix* ~ extends_primary ~ (extends_prefix* ~ extends_infix ~ extends_primary)*
}

extends_primary = _{
    expr
  | "(" ~ extends_expr ~ ")"
}

extends_prefix = _{ not }

extends_infix = _{
    extends
  | not_extends
  | equals
  | not_equals
  | strict_equals
  | strict_not_equals
  | and
  | or
}

extends = @{ "<:" }

not_extends = @{ "</:" }

equals = @{ "=" }

not_equals = @{ "!=" }

strict_equals = @{ "==" }

strict_not_equals = @{ "!==" }

and = @{ "and" }

or = @{ "or" }

not = @{ "not" }

any = @{ "any" }

unknown = @{ "unknown" }

never = @{ "never" }

primitive = {
    primitive_string
  | primitive_boolean
  | primitive_number
  | primitive_object
  | primitive_bigint
  | primitive_symbol
  | primitive_void
  | primitive_null
  | primitive_undefined
}

primitive_null = @{ "null" }

primitive_undefined = @{ "undefined" }

primitive_void = @{ "void" }

primitive_string = @{ "string" }

primitive_boolean = @{ "boolean" }

primitive_number = @{ "number" }

primitive_object = @{ "object" }

primitive_bigint = @{ "bigint" }

primitive_symbol = @{ "symbol" }

literal_true = @{ "true" }

literal_false = @{ "false" }

readonly_modifier = @{ "readonly" }

export = @{ "export" }

optional_modifier = @{ "?" }

string = { atom_string | double_quote_string | single_quote_string }

atom_string = @{ ":" ~ (!ASCII_DIGIT ~ (ASCII_ALPHANUMERIC | "$" | "_" | "-" | "/" | "\\" | "."))+ }

double_quote_string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

single_quote_string = @{ "'" ~ (!"'" ~ ANY)* ~ "'" }

template_string = @{ "`" ~ (!"`" ~ ANY)* ~ "`" }

macro_ident = @{ ident ~ "!" }

ident = @{ !(ASCII_DIGIT | keyword ~ !ident_chars) ~ ident_chars+ }

ident_chars = @{ ASCII_ALPHANUMERIC | "$" | "_" }

keyword = _{
    "as"
  | "class"
  | "const"
  | "export"
  | "extends"
  | "false"
  | "for"
  | "map"
  | "function"
  | "if"
  | "import"
  | "in"
  | "infer"
  | "interface"
  | "keyof"
  | "let"
  | "optional"
  | "readonly"
  | "never"
  | "not"
  | "then"
  | "true"
  | "type"
  | "typeof"
  | "unittest"
  | "else"
  | "end"
  | primitive
  | bottom_type
  | top_type
}

number = @{
    (neg ~ WHITESPACE*)? ~ !"_" ~ (ASCII_DIGIT | "_")+ ~ ("." ~ !"_" ~ (ASCII_DIGIT | "_")*)?
}

COMMENT       = _{ BLOCK_COMMENT | LINE_COMMENT }
BLOCK_COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
LINE_COMMENT  = _{ "//" ~ (!NEWLINE ~ ANY)* }

WHITESPACE = _{ " " | "\t" | "\n" }
